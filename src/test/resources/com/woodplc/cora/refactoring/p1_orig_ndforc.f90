!
!
!
!
      SUBROUTINE NDFORC(N,CORD,DISP,DISPT,DISP1,TD,TV,T,DICOS,TDICOS,          &
        DATA2,DATA3,ITI,GR,TX,NHARZ,DEPTH,RHOW,DRAGD,BOUYD,DEXT,DINTE,         &
        VCUR,CDNN,CDTT,CMNN,CMTT,RENO,NORE,LOCM,ADMAS,OUTP,DSFLCL,NHRZND,      &
        XLEN,IDSBN,IDNTP,FLUID,VRELP,VRELC,ITER,BOYST1,PTBOYE,IPTBY,             &
        IDSTBY,IMNPLEL,TD_MNPL,TDAX_MNPL,DIM_MNPL,IMNPL_VES,VECCG0,DSPCG,      &
        DSPCGD,DSPCGDD,TRCGD,TRCGDD,IDGSN,NPI,IFSLUG,PSLUG,DEFLOC,GDEF,        &
        DISP2,WVNUM,OMEGA,WVAMP,WVRN,DIR,NUM_SEAS,VESSEL_GRAV_ADDED,AMUD,      &
        FV_MUD_MODEL_FLAG,TA)
!
! PROGRAM TO COMPUTE LOCAL EQUIVALENT POINT LOADS FROM DISTRIBUTED LOADS
!
      USE AIR_CAN_DATA, ONLY : NACSET
      USE ANALYSIS_DATA, ONLY : IDRIFT_OFF, RECOIL_DISCONNECT_FLAG
      USE WEAKPOINT_DATA, ONLY : IWEAK_FLAG
      USE PIPE_IN_PIPE_DATA, ONLY : NPIPNOD, NPIPSECT
      USE WAVE_DATA, ONLY : NDIMWV
      USE CORIO_INERTIA, ONLY : CORIO
      USE FV_MUD_MODEL, ONLY : FV_MUD_DRAG_LOADS
      USE TIME_DATA, ONLY : START_TIME
      USE HDDIM2, ONLY : SIZE_TX
      USE RSTFIL, ONLY : FINAL_STATIC_REQUIRED
      use hddim, only : numel, nonp, nnode, ndeg, ngens, ncord, n1, n2

      IMPLICIT NONE
      !*--NDFORC1604
      !
      !*** START OF DECLARATIONS REWRITTEN BY SPAG
      !
      ! COMMON VARIABLES
      !
      REAL(8), DIMENSION(10) :: ACCX, ACCY, ACCZ, ETA, VELX, VELY, VELZ
      REAL(8), DIMENSION(40) :: DUMP, FC, FW
      REAL(8) :: EPSI, PTIME, TFINSH, TIME, TRAMP,          &
        TSTART, TSTEP, VHEAD, VISKIN
      REAL(8), DIMENSION(3) :: FCUR, FDAM, FDG, FHYD, FTHRST, FVES,     &
        FWND, VEMAS
      INTEGER :: IADM, IALL, ICABAN, ICHN, ICOG, ICROUT,                      &
        IGSCAB, IIN, INT, INTOLD, IOUT, IRAMP, IRAND, IREYN, IRSTRT,          &
        ITYPE, MAXITR, MAXRE, MXADM, MXART, MXBC, MXCAB,                      &
        MXCUR, MXDRG, MXEL, MXFREQ, MXHEAD, MXINT, MXLOAD, MXNLT, MXNOD,      &
        MXNODC, MXPAIR, MXRAOS, MXREL, MXSPR, MXSPTS, MXUDL,          &
        NADMAS, NART, NATND, NDEG1, NFREQ,                       &
        NMGSE, NMGSE0, NMGSN, NMGSN0, NMPOOL,                      &
        NODYN, NPERIOD, NPTBOY, NSRFSET,                 &
        NSURF, NTHTM, NUMBOYE, NVSET, NWVHD, NWVPASS             &
        , MXDMPER, MXWIND, MAX_AIRCAN_SETS
      INTEGER, DIMENSION(40) :: LM
      INTEGER, INTENT(IN) :: FV_MUD_MODEL_FLAG
      COMMON /AM    / NADMAS, NART
      COMMON /BCDT  / TSTART, TFINSH, TSTEP, PTIME, TIME, EPSI, MAXITR
      COMMON /BUOY  / NUMBOYE, NPTBOY
      COMMON /CABLE / ICABAN, IGSCAB
      COMMON /CONTACT/ NSURF, NSRFSET, NMGSN, NMGSN0, NMGSE, NMGSE0
      COMMON /INOUT / IIN, IOUT, IADM, IALL
      COMMON /MAXES / MXINT, MXREL, MXNOD, MXEL, MXADM, MXART, MXSPR,      &
        MXCUR, MXFREQ, MXHEAD, MXBC, MXLOAD, MXUDL, MXPAIR, MXCAB,         &
        MXNODC, MXSPTS, MXNLT, MXDRG, MXRAOS, MAXRE,     &
        MXDMPER, MXWIND, MAX_AIRCAN_SETS
      COMMON /MISC  / FC, FW, DUMP, LM, ITYPE, INT, ICROUT
      COMMON /MOONPOOL/ NMPOOL
      COMMON /NONDYN/ NODYN
      COMMON /RAMP  / TRAMP, IRAMP
      COMMON /REYNOLD/ VISKIN, IREYN, NWVPASS, NPERIOD
      COMMON /SEAMUS/ INTOLD, ICHN
      COMMON /START / IRSTRT, IRAND, NFREQ, NDEG1, NATND, NWVHD, NVSET
      COMMON /THETM / NTHTM
      COMMON /VESSEL/ VHEAD, ICOG
      COMMON /VFORCES/ FVES, FHYD, FDAM, FWND, FCUR, FTHRST, FDG, VEMAS
      COMMON /WAVE  / VELX, VELY, VELZ, ACCX, ACCY, ACCZ, ETA
      !
      ! DUMMY ARGUMENTS
      !
      REAL(8) :: AMUD, BOUYD, DATA2, DATA3, DEPTH, DEXT, DINTE, DRAGD,   &
        GR, RHOW
      INTEGER :: IDNTP, IDSTBY, IFSLUG, IMNPLEL, ITER, ITI, N, NUM_SEAS
      LOGICAL :: VESSEL_GRAV_ADDED
      REAL(8), DIMENSION(NADMAS, NDEG) :: ADMAS
      REAL(8), DIMENSION(3, 3) :: BOYST1
      REAL(8), DIMENSION(MAXRE) :: CDNN, CDTT, CMNN, CMTT, RENO
      REAL(8), DIMENSION(NCORD, NONP) :: CORD
      REAL(8), DIMENSION(9) :: DEFLOC
      REAL(8), DIMENSION(7) :: PSLUG
      REAL(8), DIMENSION(NDEG, NDEG) :: DICOS, TDICOS
      REAL(8), DIMENSION(NMPOOL) :: DIM_MNPL
      REAL(8), DIMENSION(NDIMWV) :: DIR, OMEGA, WVAMP, WVNUM, WVRN
      REAL(8), DIMENSION(N1) :: DISP, DISP1, DISP2, DISPT
      REAL(8), DIMENSION(NUMEL, N1) :: DSFLCL
      REAL(8), DIMENSION(6, MXRAOS) :: DSPCG
      REAL(8), DIMENSION(3, MXRAOS) :: DSPCGD, DSPCGDD, VECCG0
      REAL(8), DIMENSION(NUMEL, 5) :: FLUID
      REAL(8), DIMENSION(NTHTM, N1) :: GDEF
      INTEGER, DIMENSION(NONP, NSURF) :: IDGSN
      INTEGER, DIMENSION(NONP) :: IDSBN, NHRZND
      INTEGER, DIMENSION(NMPOOL) :: IMNPL_VES
      INTEGER, DIMENSION(NPTBOY, 2) :: IPTBY
      INTEGER, DIMENSION(NADMAS, 2) :: LOCM
      INTEGER, DIMENSION(N2) :: NHARZ
      INTEGER, DIMENSION(MAXRE) :: NORE
      INTEGER, DIMENSION(NNODE, NUMEL) :: NPI
      REAL(8), DIMENSION(NDEG, NONP, 6) :: OUTP
      REAL(8), DIMENSION(NPTBOY, 4, 3) :: PTBOYE
      REAL(8), DIMENSION(NGENS, N1) :: T
      REAL(8), DIMENSION(NDEG, NONP) :: TD, TV
      REAL(8), DIMENSION(NMPOOL, 2, 3) :: TDAX_MNPL
      REAL(8), DIMENSION(NMPOOL, 3) :: TD_MNPL
      REAL(8), DIMENSION(3, 3, MXRAOS) :: TRCGD, TRCGDD
      REAL(8), DIMENSION(SIZE_TX, N1) :: TX
      REAL(8), DIMENSION(NONP, 3) :: VCUR
      REAL(8), DIMENSION(INT) :: VRELC, VRELP
      REAL(8), DIMENSION(NDEG, NONP), INTENT(IN) :: TA
      REAL(8), DIMENSION(NUMEL), INTENT(IN) :: XLEN
      INTENT (IN) ADMAS, IDNTP, LOCM
      INTENT (INOUT) DISP1, DSFLCL, OUTP, TX, VESSEL_GRAV_ADDED
      !
      ! LOCAL VARIABLES
      !
      REAL(8) :: BRAMP, CC, CRAMP, DINT, ELV, PRSINT, PXE, PXS, RAMP,      &
        RHOM, SALTMP, WRAMP, XNN, XL
      REAL(8), DIMENSION(10) :: HTRANS
      INTEGER :: I, I1, I2, ID, IDP3, IJ, IM, IN, IND, IPOS, IVSET,       &
        NUMINT,OUTER_ELEM, PIP_SECT_NO
      INTEGER, DIMENSION(10) :: IPHYD
      REAL(8), DIMENSION(10, 3) :: WACC, WVEL
      LOGICAL :: INNER_PIP_ELEM

      ! Initialise local variables



      INNER_PIP_ELEM = .FALSE.
      OUTER_ELEM = 0
      PIP_SECT_NO = 0
      XL = XLEN(N)
      !
      !*** END OF DECLARATIONS REWRITTEN BY SPAG

      RHOM   = FLUID(N,3)
      PRSINT = FLUID(N,4)
!
! CLEAR TOTAL LOAD VECTOR DISPT : TOTAL ACCUMULATOR
!
      CC = 0.D0
      CALL SCLA(DISPT,CC,N1,1)
!
! SET RAMP VALUE FOR LOAD COMPUTATIONS
!
      RAMP = 1.D0
      IF(TIME.LT.TRAMP.AND.IRSTRT.LT.2)CALL CALRMP(TIME,TRAMP,START_TIME,RAMP,    &
        IRAMP)
      BRAMP = RAMP
      WRAMP = RAMP
      CRAMP = RAMP
      IF(ICABAN.EQ.1)THEN
        BRAMP = 1.D0
        WRAMP = 1.D0
        CRAMP = 1.D0
      END IF

      IF(IDNTP.NE.1.AND.IDNTP.NE.3)GOTO 100
!
! FIRST COMPUTE GAUSSIAN WEIGHTS AND COORDINATES
!
      IPOS = 0

10    CALL POSWT(N,ITI,XL)
      IPOS = IPOS + 1
!
! CALL WAVE GENERATOR ROUTINE
! WAVEM IS ENTERED FOR THE CASE OF NH = 0 TO ZERO OUT ARRAYS
!
      IND = 1

      IF(ICHN.EQ.1)THEN
        NUMINT = INTOLD
      ELSE
        NUMINT = INT
      END IF

      CALL WAVEM(N,TD,NHARZ,DEPTH,GR,TIME,CORD,DRAGD,IND,NHRZND,   &
                XL,WVNUM,OMEGA,WVAMP,WVRN,DIR)
!
!.... If appropriate, check if element is within vessel moonpool and
!.... find corresponding water particle velocities & accelerations
!
      IF (IMNPLEL == 1) THEN
        CALL MOONPOOL_HYDRODYNAMICS(CORD,TD,XL,DEPTH,TD_MNPL,TDAX_MNPL,      &
       DIM_MNPL,IMNPL_VES,VECCG0,DSPCG,DSPCGD,DSPCGDD,TRCGD,TRCGDD,WVEL,     &
       WACC,IPHYD,HTRANS)
      END IF

      IF(IPOS.GT.1)GOTO 40
!
!.... Determine if element is surface-piercing. If element is subject to
!.... moonpool hydrodynamics, the free-surface elevation is based on the
!.... depth alone - wave action is ignored.
!
      IND  = 0
      ICHN = 0
      DO 30 IN = 1,NNODE
        IJ = LM(IN)
        XNN = TD(1,IJ) + CORD(1,IJ)
        IF (IMNPLEL == 0) THEN
          ELV = DEPTH + ETA(1)
          IF(IN.EQ.NNODE)ELV = DEPTH + ETA(INT)
        ELSE
          ELV = DEPTH
        END IF
        IF(XNN.LT.ELV)IND = IND + 1
30    CONTINUE

      IF(IND.NE.1 .OR. FINAL_STATIC_REQUIRED)GOTO 40

      INTOLD = INT
      INT  = 10
      IF(IREYN.EQ.1)INT=INTOLD ! IF Cd=f(Re)=> #INTEGRATION PTS CONSTANT
      ICHN = 1
      GOTO 10

40    CONTINUE
!
!
! TEST TO SEE IF STRUCTURE IS IN WATER: IS FLUID DENSITY FINITE?
!
      IF(RHOW.LT.1.0D-8.AND.RHOM.LT.1.0D-8.AND.PRSINT.LT.1.0D-8)GOTO 100
!
! CLEAR ARRAY DISP1 FOR ACCUMULATION OF LOCAL FLUID LOADS
!
      CALL SCLA(DISP1,CC,N1,1)

      ! Check if the present element is the inner element of a pipe-in-pipe pair.
      ! If the outer element exists (>0) then this is an inner Pipe-In-Pipe element.
      INNER_PIP_ELEM = .FALSE.
      IF( NPIPSECT > 0 )THEN
        CALL FIND_OUTER_PIP_ELEM(N,NUMEL,OUTER_ELEM,PIP_SECT_NO)
        IF( OUTER_ELEM > 0 )then
          INNER_PIP_ELEM = .TRUE.
        end if
      END IF

!
! CALCULATE DRAG AND INERTIA FACTORS DEPENDENT ON BUOYANCY DIAMETER
!
      CALL CALCULATE_DRAG_AND_INERTIA_FACTORS(N,TD,CORD,DISP,XL,     &
        DEPTH,BOUYD,DEXT,IDSTBY)
!
! NODAL LOADS DUE TO FLUID STRUCTURE INTERACTION -- MORISON'S EQ.
!
      CALL MORSON(N,TV,T,CORD,DISP1,DISP,XLEN,VCUR,CDNN,CDTT,CMNN,CMTT,     &
        RENO,NORE,DRAGD,RHOW,DEPTH,TD,DICOS,IDSBN,VRELP,VRELC,ITER,       &
        BOYST1,IDGSN,IMNPLEL,WVEL,WACC,IPHYD,HTRANS,WVNUM,OMEGA,WVAMP,    &
        WVRN,DIR,NUM_SEAS,GR,BOUYD, OUTER_ELEM, INNER_PIP_ELEM,           &
        PIP_SECT_NO,FLUID,NPI, TA)
!
! ACCUMULATE LOCAL DISTRIBUTED FORCES IN DSFLCL
!
      DO 60 IN = 1,N1
        SALTMP = DISP1(IN)*RAMP
        DSFLCL(N,IN) = DSFLCL(N,IN) - SALTMP
60    CONTINUE
!
! ACCUMULATE FLUID FORCES IN DISPT
!
      CALL TRANSF(DISP1,DISP,TDICOS)     ! LOCAL TO GLOBAL
!
! POINT BUOY NODAL LOADS DUE TO MORISON'S EQ. (GLOBAL AXES)
!
      IF(NPTBOY.GT.0)CALL POINTBOY(1,PTBOYE,IPTBY,DISP1,N,TV,TD,CORD,   &
        DEPTH,RHOW,VCUR)

      DO 61 IN = 1,N1
        DISP1(IN) = DISP1(IN)*RAMP
61    CONTINUE

      CALL GMADD(DISP1,DISPT,DISPT,N1,1)

      DO 70 ID = 1,NDEG
        IDP3 = ID + NDEG
        OUTP(ID,LM(1),1) = OUTP(ID,LM(1),1) - DISP1(ID)
        OUTP(ID,LM(2),1) = OUTP(ID,LM(2),1) - DISP1(IDP3)
70    CONTINUE
!
! NODAL BUOYANCY FORCES -- ROTATION  AND DEFORMATION
!
      CALL SCLA(DISP1,CC,N1,1)

      CALL BOYANC(N,TD,CORD,DISP1,DISP,XL,GR,DEPTH,BOUYD,DEXT,DINTE,          &
        RHOW,T,PXS,PXE,DICOS,IDSBN,FLUID,IDSTBY,IDGSN,IFSLUG, PSLUG,          &
        DEFLOC,FV_MUD_MODEL_FLAG,INNER_PIP_ELEM,OUTER_ELEM)

      IF(IDSTBY.EQ.1)THEN
        CALL SCLA(DISP2,CC,N1,1)
        CALL BOYDIST(DISP2,DISP,GR,XL,DICOS,T,N,IDSBN,TD,CORD,BOUYD,      &
                     DEXT,DEPTH,RHOW,IDGSN,INNER_PIP_ELEM)
        CALL GMADD(DISP2,DISP1,DISP1,N1,1)
      END IF
!
! ACCUMULATE DISTRIBUTED LOADS IN DSFLCL
!
      DO 80 IN = 1,N1
        SALTMP = DISP1(IN)*BRAMP
        DSFLCL(N,IN) = DSFLCL(N,IN) - SALTMP
80    CONTINUE

      DSFLCL(N,1) = DSFLCL(N,1) - PXS*BRAMP
      DSFLCL(N,7) = DSFLCL(N,7) + PXE*BRAMP
!
! ACCUMULATE IN DISPT
!
      CALL TRANSF(DISP1,DISP,TDICOS)     ! LOCAL TO GLOBAL
!
! POINT BUOY NODAL LOADS DUE TO BUOYANCY (GLOBAL AXES)
!
      IF(NPTBOY.GT.0)CALL POINTBOY(2,PTBOYE,IPTBY,DISP1,N,TV,TD,CORD,    &
        DEPTH,RHOW,VCUR)
!
! NODAL LOADS DUE TO BUOYANCY FROM AIR CANS(GLOBAL AXES)
!
      IF(NACSET.GT.0)CALL AIRCAN_BUOY(DEPTH,DISP1,N,TD,CORD,NPI,RHOW,GR,   &
        ITER)

      DO 81 IN = 1,N1
        DISP1(IN) = DISP1(IN)*BRAMP
81    CONTINUE

      CALL GMADD(DISP1,DISPT,DISPT,N1,1)

      DO 90 ID = 1,NDEG
        IDP3 = ID + NDEG
        OUTP(ID,LM(1),2) = OUTP(ID,LM(1),2) - DISP1(ID)
        OUTP(ID,LM(2),2) = OUTP(ID,LM(2),2) - DISP1(IDP3)
90    CONTINUE

100   CONTINUE
!
! INCLUDE GRAVITY EFFECTS IF ACCELERATION CONSTANT FINITE
!
      IF(GR.LT.1.0D-08)GOTO 150
!
! FIRST COMPUTE DISTRIBUTED GRAVITY LOADS FOR BEAM ELEMENTS ONLY
!
      CALL SCLA(DISP1,CC,N1,1)

      IF(IDNTP.EQ.1.OR.IDNTP.EQ.3)THEN

        CALL APPWT(DISP1,DISP,GR,DATA2,DATA3,XL,DICOS,T,N,IDSBN,TD,CORD,   &
                   IDGSN,IFSLUG,PSLUG,FLUID,DINTE,AMUD,FV_MUD_MODEL_FLAG)
!
! ACCUMULATE NODAL LOADS IN DSFLCL
!
        DO 110 ID = 1,N1
          SALTMP = DISP1(ID)*WRAMP
          DSFLCL(N,ID) = DSFLCL(N,ID) - SALTMP
110     CONTINUE
!
! ROTATE NODAL FORCES TO GLOBAL
!
        CALL TRANSF(DISP1,DISP,TDICOS)   ! LOCAL TO GLOBAL

      END IF
!
! POINT BUOY NODAL LOADS DUE TO GRAVITY (GLOBAL AXES)
!
      IF(NPTBOY.GT.0)CALL POINTBOY(3,PTBOYE,IPTBY,DISP1,N,TV,TD,CORD,   &
        DEPTH,RHOW,VCUR)
!
! ADD IN GRAVITY FORCES DUE TO POINT MASSES
!
      IF(NADMAS.EQ.1)GOTO 130

      DO 120 IM = 2,NADMAS
!
! CHECK IF THIS ELEMENT HAS ADDED MASS
!
        IF(N.NE.LOCM(IM,1))GOTO 120
!
! CHECK ON NODE WITH ADDED MASS
!
        IF(LM(2).EQ.LOCM(IM,2))GOTO 115
!
! ADDED MASS ON FIRST ELEMENT NODE
!
        DISP1(1) = DISP1(1) + ADMAS(IM,1)*GR
        GOTO 120
!
! ADDED MASS ON SECOND ELEMENT NODE
!
115     CONTINUE

        DISP1(7) = DISP1(7) + ADMAS(IM,1)*GR

120   CONTINUE

130   CONTINUE
!
! Add in gravity force on vessel if this is a drift-off simulation
!
      IF ((IWEAK_FLAG==1.OR.IDRIFT_OFF==1).AND.                        &
                                    (.NOT.VESSEL_GRAV_ADDED)) THEN
        IVSET = 1
        I1 = NPI(1,N)
        I2 = NPI(2,N)
        IF (I1.EQ.ICOG) THEN
          DISP1(1) = DISP1(1) + (VEMAS(1) * GR)
          VESSEL_GRAV_ADDED = .TRUE.
        ELSE IF (I2.EQ.ICOG) THEN
          DISP1(7) = DISP1(7) + (VEMAS(1) * GR)
          VESSEL_GRAV_ADDED = .TRUE.
        END IF
      END IF

      DO 131 IN = 1,N1
        DISP1(IN) = DISP1(IN)*WRAMP
131   CONTINUE
!
! ACCUMULATE IN DISPT
!
      CALL GMADD(DISP1,DISPT,DISPT,N1,1)

      DO 140 ID = 1,NDEG
        IDP3 = ID + NDEG
        OUTP(ID,LM(1),3) = OUTP(ID,LM(1),3) - DISP1(ID)
        OUTP(ID,LM(2),3) = OUTP(ID,LM(2),3) - DISP1(IDP3)
140   CONTINUE

150   CONTINUE
!
! MODIFICATION TO INCLUDE EFFECT OF CORIOLIS FORCES DUE TO
! INTERNAL FLOW / SLUG
!
      IF(NODYN.EQ.0.AND.CORIO(N).EQ.1)THEN
        CALL SCLA(DISP1,CC,N1,1)

        CALL CORIOL(TV,DICOS,PSLUG,FLUID,DISP,T,N,XL,IDSBN,IFSLUG,  &
        DINTE,DISP1,GDEF,DEFLOC)
!
! ACCUMULATE NODAL LOADS IN DSFLCL
!
       DO ID = 1,N1
        SALTMP = DISP1(ID)*CRAMP
        DSFLCL(N,ID) = DSFLCL(N,ID) - SALTMP
       END DO
!
! ROTATE NODAL FORCES TO GLOBAL
!
       CALL TRANSF(DISP1,DISP,TDICOS)
!
! ACCUMULATE IN DISPT
!
       DO IN = 1,N1
         DISP1(IN) = DISP1(IN)*CRAMP
       END DO

       CALL GMADD(DISP1,DISPT,DISPT,N1,1)

       DO ID = 1,NDEG
         IDP3 = ID + NDEG
         OUTP(ID,LM(1),2) = OUTP(ID,LM(1),2) - DISP1(ID)
         OUTP(ID,LM(2),2) = OUTP(ID,LM(2),2) - DISP1(IDP3)
       END DO

      END IF

      ! Include drag loads on riser due to drilling mud evacuation in a
      ! recoil analysis, if appropriate. This is only done for the second
      ! (disconnected) phase of a dynamic recoil analysis.
      IF (FV_MUD_MODEL_FLAG == 1 .AND. RECOIL_DISCONNECT_FLAG == 2) THEN

        CALL FV_MUD_DRAG_LOADS(N1,NGENS,NCORD,NDEG,NONP,N,INT,LM,FC,FW,    &
          CORD,TD,TV,XL,TDICOS,T,DISP1)

        ! Accumulate nodal loads in DSFLCL. Note that no ramp is applied
        ! to the mud drag loads - this would make no sense for a recoil
        ! analysis
        DSFLCL(N,1:N1) = DSFLCL(N,1:N1) - DISP1(1:N1)

        ! Rotate local nodal forces to global axis system
        CALL TRANSF(DISP1,DISP,TDICOS)

        ! Accumulate in DISPT
        CALL GMADD(DISP1,DISPT,DISPT,N1,1)

      END IF
!
! PLACE ELEMENT NODAL FORCES INTO GLOBAL FORCE ARRAY TX
!
      DO 190 I = 1,N1
        SALTMP = DISPT(I)
        TX(N,I) = TX(N,I) - SALTMP
190   CONTINUE

      IF(ICHN.EQ.1)INT = INTOLD
!
! DEBUG OUTPUT FOR TOTAL GLOBAL NODAL LOADS
!
      IF(IALL.LT.2)RETURN

      WRITE(IOUT,200)N
      CALL MXOUT(N,DISPT,N1,1,0,60,120,1,IOUT)

      RETURN

200   FORMAT(2X,' TOTAL EQUIVALENT NODAL FORCES DUE TO ALL SOURCES ', &
        'FOR ELEMENT NO. = ',I5//)

      END